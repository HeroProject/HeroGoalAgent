use '../util/*.pl' as knowledge.
use '../minidialogs/*.pl' as knowledge.
use '../agent_actions' as actionspec.

order=linearall.

module initializeUserModel {
	% Initialize user model
	if true then insert(userModel([]), dialogHistory([[]]), narrativeHistory([]), topicsOfInterest([])).

	forall bel(localVariable(Key, Value))
		do {
			if bel(userModel(UserModel), updateUserModel(Key, Value, UserModel, NewUserModel))
				then delete(userModel(UserModel)) + insert(userModel(NewUserModel)).
		}
	
	% Synchronize with remote user model and history
	if bel(useMemory(true)) then {
		if bel(userId(UserId), sessionId(SessionId)) then setSession(UserId, SessionId).
		if bel(continueSession(true), userId(UserId)) then getDialogHistory(UserId) + getNarrativeHistory(UserId) + getTopicsOfInterest(UserId) + insert(waitingForHistory).
		if bel(continueSession(false), userId(UserId), sessionId(SessionId)) then clearHistory(UserId,SessionId) + insert(waitingForHistory).
	}
	
	
	if true then delete(waitingForInit) + insert(waitingForSession).
}
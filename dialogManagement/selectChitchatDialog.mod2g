use '../util/*.pl' as knowledge.
use '../minidialogs/*.pl' as knowledge.
use '../hero_actions' as actionspec.

order=linear.

module selectChitchatDialog(SessionElement, RemainingElements) {

	% Select a new minidialog of a topic with depedencies.
	if bel(SessionElement = [topic=Topic],
			minidialog(Minidialog, [type=chitchat, theme=_, topic=Topic]), not(isInDialogHistory(Minidialog, _)),
			dependencies(Minidialog, Dependencies), matchesDepencencies(Dependencies))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Select a new minidialog of a topic without dependencies.
	if bel(SessionElement = [topic=Topic],
			minidialog(Minidialog, [type=chitchat, theme=_, topic=Topic]), not(isInDialogHistory(Minidialog, _)),
			not(dependencies(Minidialog, _)))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Select a new minidialog of a theme with dependencies.
	if bel(SessionElement = [theme=Theme], Theme \= '_chitchat',
			minidialog(Minidialog, [type=chitchat, theme=Theme, topic=_]), not(isInDialogHistory(Minidialog, _)),
			dependencies(Minidialog, Dependencies), matchesDepencencies(Dependencies))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Select a new minidialog of a theme without dependencies.
	if bel(SessionElement = [theme=Theme], Theme \= '_chitchat',
			minidialog(Minidialog, [type=chitchat, theme=Theme, topic=_]), not(isInDialogHistory(Minidialog, _)),
			not(dependencies(Minidialog, _)))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Select a new minidialog based with a topic from the known topics of interest.
	if bel(SessionElement = [theme='_chitchat'], findSuitableChitchat(Minidialog, topic))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Select a new minidialog based with a theme from the known topics of interest.
	if bel(SessionElement = [theme='_chitchat'], findSuitableChitchat(Minidialog, theme))
		then insert(selectedMinidialog(Minidialog, RemainingElements)).
	
	% Todo: decide whether or not to insert a random topic when theme='_chitchat'
}
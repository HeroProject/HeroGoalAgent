use '../util/*.pl' as knowledge.
use '../minidialogs/*.pl' as knowledge.
use '../minidialogs/co_creation/*.pl' as knowledge.
use '../minidialogs/robotstories/*.pl' as knowledge.
use '../minidialogs/hospital/*.pl' as knowledge.
use '../sessions' as knowledge.
use '../hero_actions' as actionspec.

order=linearall.

% Performs all action (modalities) at the start of a move Move for minidialog Minidialog.
module performDialogMove(Minidialog, Move) {

	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%				DIALOG ATTRIBUTES				%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	% Say what needs to be said.
	if bel(text(Minidialog, Move, Text), keyValue(Minidialog, Move, speechSpeed, Speed), replaceVar(Text, Text2), addSpeechSpeed(Text2, Speed, TextProcessed)) then 
	{
		if bel(keyValue(Minidialog, Move, noAnimation, "true")) then say(TextProcessed).
		if not(bel(keyValue(Minidialog, Move, noAnimation, "true"))) then sayAnimated(TextProcessed).
	}

	% Led there be light.
	if bel(leds(Minidialog, Move, direct, Leds, Colors), valueListFromKeyList(Colors, ColorsR)) then setLedColor(Leds, ColorsR).
	if bel(leds(Minidialog, Move, direct, Leds, AnimType, Colors, Speed)) then startLedAnimation(Leds, AnimType, Colors, Speed).
	if bel(leds(Minidialog, Move, id, ID), getUserModelValue(ID, [Leds, colorSelect, Colors, _])) then setLedColor(Leds, Colors).
	if bel(leds(Minidialog, Move, id, ID), getUserModelValue(ID, [Leds, AnimType, Colors, Speed]), AnimType \= colorSelect) then startLedAnimation(Leds, AnimType, Colors, Speed).
	if bel(leds(Minidialog, Move, option, OptionNumber), waitingForLedAnim(ID), animOption(ID, OptionNumber, [Leds, colorSelect, Colors, _])) then setLedColor(Leds, Colors).
	if bel(leds(Minidialog, Move, option, OptionNumber), waitingForLedAnim(ID), animOption(ID, OptionNumber, [Leds, AnimType, Colors, Speed]), AnimType \= colorSelect) then startLedAnimation(Leds, AnimType, Colors, Speed).
	if bel(leds(Minidialog, Move, reset)) then stopLedAnimation.

	% Play motions.
	if bel(anim(Minidialog, Move, onRobot, Anim)) then gesture(Anim).
	if bel(anim(Minidialog, Move, file, Anim)) then playMotion(Anim).
	if bel(anim(Minidialog, Move, id, ID), getUserModelValue(ID, Recording)) then playMotion(Recording).
	if bel(anim(Minidialog, Move, option, OptionNumber), waitingForMotionAnim(ID), animOption(ID, OptionNumber, Motion)) then playMotion(Motion).
	
	% Play audio.
	if bel(audio(Minidialog, Move, file, A)) then playAudio(A).
	if bel(audio(Minidialog, Move, id, ID), getUserModelValue(ID, AudioID)) then playAudio(AudioID).
	if bel(audio(Minidialog, Move, option, OptionNumber), waitingForSoundAnim(ID), animOption(ID, OptionNumber, Sound)) then playAudio(Sound).
	
	% Wake up and rest.
	if bel(wakeup(Minidialog, Move)) then wakeUp.
	if bel(rest(Minidialog, Move)) then rest + stopLedAnimation.
	
	% Set breathing
	if bel(enableBreathing(Minidialog, Move), basePosture('Stand')) then enableBreathing.
	if bel(enableBreathing(Minidialog, Move), basePosture('Sit')) then enableBreathing('Head').
	if bel(disableBreathing(Minidialog, Move)) then disableBreathing.
	
	% Go to posture
	if bel(go_to_posture(Minidialog, Move, Posture, Speed)) then goToPosture(Posture, Speed).
	if bel(go_to_posture(Minidialog, Move, Posture)) then goToPosture(Posture).
	if bel(go_to_posture(Minidialog, Move), basePosture(Posture)) then goToPosture(Posture).
	
	% Set stiffness
	if bel(set_stiffness(Minidialog, Move, Joints, Stiffness)) then setStiffness(Joints, Stiffness).
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%					WEBPAGE						%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	% Show text.
	if bel(display(Minidialog, Move, Txt), text(Txt, HTxt), html(HTxt, Html), currentAttempt(1)) 
		then renderPage(Html).
	if bel(display_raw(Minidialog, Move, Txt), html(Txt, Html), currentAttempt(1)) 
		then renderPage(Html).
	% Show image.
	if bel(image(Minidialog, Move, I), img(I, Img), html(Img, Html), currentAttempt(1)) 
		then renderPage(Html).
	% Show questions
	if bel(text(Minidialog, Move, Txt), keyValue(Minidialog, Move, type, yesno), text(Txt, HTxt)) then {
		%if bel(currentAttempt(1), html(HTxt, Html))
		%	then renderPage(Html).
		if bel(/*currentAttempt(N), N>1,*/ buttons(['Ja', 'Nee'], B), atom_concat(HTxt, B, TandB), html(TandB, Html))
			then renderPage(Html).
	}
	if bel(text(Minidialog, Move, Txt), keyListValues(Minidialog, Move, [type, options], [quiz, List]), text(Txt, HTxt)) then {
		%if bel(currentAttempt(1), html(HTxt, Html))
		%	then renderPage(Html).
		if bel(/*currentAttempt(N), N>1,*/ buttons(List, B), atom_concat(HTxt, B, TandB), html(TandB, Html))
			then renderPage(Html).
	}
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%					CO-CREATION					%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%%%% AUDIO %%%%
	% Prepare co-creation of audio
	if bel(prepare_sound_anim(Minidialog, Move), keyListValues(Minidialog, Move, [umVariable, option1, option2, recordTime], [ID, Option1, Option2, RT]), session(Session))
		then delete(session(Session)) + insert(session([co_sound | Session]), waitingForSoundAnim(ID), animOption(ID, 1, Option1), animOption(ID, 2, Option2), soundConfig(ID, RT)).
	
	% Save result of audio co-creation
	if bel(save_sound(Minidialog, Move, ID, Sound), userModel(UserModel), updateUserModel(ID, Sound, UserModel, NewUserModel), userId(UserId)) then
			delete(userModel(UserModel)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, Sound).
	
	if bel(save_sound(Minidialog, Move, OptionNumber), waitingForSoundAnim(ID), animOption(ID, OptionNumber, Sound), soundConfig(ID, RT)) then {
		if bel(userModel(UserModel), updateUserModel(ID, Sound, UserModel, NewUserModel), userId(UserId)) then
			delete(soundConfig(ID, RT), waitingForSoundAnim(ID), userModel(UserModel)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, Sound).
		
		forall bel(animOption(ID, Number, Option)) do delete(animOption(ID, Number, Option)).
	}
	
	%%%%% MOTIONS %%%%
	% Prepare co-creation of a motion
	if bel(prepare_motion_anim(Minidialog, Move), keyListValues(Minidialog, Move, [umVariable, option1, option2], [ID, Option1, Option2]), session(Session))
		then delete(session(Session)) + insert(session([co_motion | Session]), waitingForMotionAnim(ID), animOption(ID, 1, Option1), animOption(ID, 2, Option2)).
	
	% Co-create motion
	if bel(record_motion(Minidialog, Move, Joints, Duration), Duration > 0, (keyValue(Minidialog, Move, umVariable, ID) ; not(keyValue(Minidialog, Move, umVariable, _)), generateKeyFromMinidialogAndMove(Minidialog, Move, ID))) 
		then insert(waitingForMotionRecording(ID)) + setStiffness(Joints, 0) + startMotionRecording(Joints) + starttimer(recordMotion, Duration, Duration).
		
	if bel(record_motion(Minidialog, Move, Joints, 0))
		then setStiffness(Joints, 0) + startMotionRecording(Joints).
	
	% Save result of motion co-creation
	if bel(save_motion(Minidialog, Move, option, OptionNumber), waitingForMotionAnim(ID), animOption(ID, OptionNumber, Motion)) then {
		if bel(userModel(UserModel), updateUserModel(ID, Motion, UserModel, NewUserModel), userId(UserId)) then
			delete(userModel(UserModel), waitingForMotionAnim(ID)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, Motion).
		
		forall bel(animOption(ID, Number, Option)) do delete(animOption(ID, Number, Option)).
	}
	
	if bel(save_motion(Minidialog, Move, recording, Joints), (waitingForMotionAnim(ID); not(waitingForMotionAnim(_)), keyValue(Minidialog, Move, umVariable, ID); not(waitingForMotionAnim(_)), not(keyValue(Minidialog, Move, umVariable, _)), generateKeyFromMinidialogAndMove(Minidialog, Move, ID)))
		then {
			if true then stopMotionRecording + setStiffness(Joints, 100) + insert(waitingForMotionRecording(ID)).
			
			if bel(waitingForMotionAnim(ID)) then {
				if true then delete(waitingForMotionAnim(ID)).
				forall bel(animOption(ID, Number, Option)) do delete(animOption(ID, Number, Option)).
			}
		}
	
	%%%% LED ANIMATIONS %%%%
	% Prepare co-creation of led animation. Set a flag that the next led animation will be linked to this id.
	if bel(prepare_led_anim(Minidialog, Move), not(keyValue(Minidialog, Move, umVariable, _)), generateKeyFromMinidialogAndMove(Minidialog, Move, ID), session(Session))
		then delete(session(Session)) + insert(session([co_led_selection | Session]), waitingForLedAnim(ID)).
	
	if bel(prepare_led_anim(Minidialog, Move), keyListValues(Minidialog, Move, [umVariable, option1, option2], [ID, Option1, Option2]), session(Session))
		then delete(session(Session)) + insert(session([co_led_selection | Session]), waitingForLedAnim(ID), animOption(ID, 1, Option1), animOption(ID, 2, Option2)).
	
	if bel(prepare_led_anim(Minidialog, Move), keyValue(Minidialog, Move, umVariable, ID), not(keyListValues(Minidialog, Move, [option1, option2], [_, _])), session(Session))
		then delete(session(Session)) + insert(session([co_led_creation | Session]), waitingForLedAnim(ID)).

	% Save the led animation configurations.
	% Remove all the downloaded options
	forall bel(save_led_anim(Minidialog, Move, _, _, _, _), waitingForLedAnim(ID), animOption(ID, Number, Option)) do delete(animOption(ID, Number, Option)).
		
	%Save a blinking, alternate, and spinning animations.
	if bel(save_led_anim(Minidialog, Move, Group, AnimType, Colors, Speed), waitingForLedAnim(ID), not(AnimType = colorSelect),
	getUserModelValue(Group, GroupR), getUserModelValue(AnimType, AnimTypeR), valueListFromKeyList(Colors, ColorsR), 
	userModel(UserModel), updateUserModel(ID, [GroupR, AnimTypeR, ColorsR, Speed], UserModel, NewUserModel),
	nested_list_to_string([GroupR, AnimTypeR, ColorsR, Speed], AnimString), userId(UserId)) 
		then delete(userModel(UserModel), waitingForLedAnim(ID)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, AnimString).
	
	%Save a color select animation.
	if bel(save_led_anim(Minidialog, Move, Group, AnimType, Colors, Speed), waitingForLedAnim(ID), AnimType = colorSelect, valueListFromKeyList(Colors, ColorsR), 
	userModel(UserModel), updateUserModel(ID, [Group, AnimType, ColorsR, Speed], UserModel, NewUserModel),
	nested_list_to_string([Group, AnimType, ColorsR, Speed], AnimString), userId(UserId)) 
		then delete(userModel(UserModel), waitingForLedAnim(ID)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, AnimString).
	
	% Save a led animation option as selected option and remove the remaining options.
	if bel(save_led_anim(Minidialog, Move, OptionNumber), waitingForLedAnim(ID), animOption(ID, OptionNumber, [Group, AnimType, Colors, Speed])) then 
	{
		%Store the selected uptions with the right ID in the local and remote user model.
		if bel(userModel(UserModel), updateUserModel(ID, [Group, AnimType, Colors, Speed], UserModel, NewUserModel),
				nested_list_to_string([Group, AnimType, Colors, Speed], AnimString), userId(UserId)) 
			then delete(userModel(UserModel), waitingForLedAnim(ID)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, AnimString).
		
		%Remove all the stored led animation options.
		forall bel(animOption(ID, Number, Option)) do delete(animOption(ID, Number, Option)).
	} 
	
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%				MINIDIALOG MANAGEMENT			%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	forall bel(insert_minidialog(Minidialog, Move, NestedMinidialog)) do {
    	if bel(session(Session)) then delete(session(Session)) + insert(session([NestedMinidialog | Session])).
	}
	
	if bel(remove_minidialog(Minidialog, Move, MinidialogsToDelete), session(Session), delete_minidialogs(Session, MinidialogsToDelete, UpdatedSession)) then delete(session(Session)) + insert(session(UpdatedSession)).

	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%				 	OTHER						%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%Active eventFailSave if required.	
	if bel(evenFailSafeActive(true)) then starttimer(eventFailSafe, 16000, 16000).
	
	if bel(recordInteractionActive(true)) then {
		if bel(start_interaction_recording(Minidialog, Move), interaction_recording_id(ID)) then startInteractionRecording(ID).
		if bel(stop_interaction_recording(Minidialog, Move)) then stopInteractionRecording.
		if bel(start_interaction_probe(Minidialog, Move), probes(Count), NewCount is Count + 1, interaction_probe_id(NewCount, ID)) then delete(probes(Count)) + insert(probes(NewCount)) + startInteractionProbe(ID).
		if bel(stop_interaction_probe(Minidialog, Move)) then stopInteractionProbe.
	}
	
	
	%Remove start to prevent repeating the execution of this module (again) and record that robot action has started.
	if true then delete(start) + insert(started).
}
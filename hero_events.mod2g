use 'util/*.pl' as knowledge.
use 'topics/*.pl' as knowledge.
use sessions as knowledge.
use hero_actions as actionspec.
use 'recognitionAndRepairePipeline/askQuestion' as module.
use 'recognitionAndRepairePipeline/processUserResponse' as module.
use 'recognitionAndRepairePipeline/repair' as module.
use 'recognitionAndRepairePipeline/selectRobotResponse' as module.
use 'stateLogic/performStateTransition' as module.
use 'memory/handleMemoryData' as module.

module hero_events {
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Percept and event processing      %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	% Robot event percepts
	forall percept(event(E)), not(bel(event(E)))
		do insert(event(E)).
	forall bel(event(E)), not(percept(event(E)))
		do delete(event(E)).
		
	% Memory data percepts
	forall percept(memoryData(Key, Value))
		do handleMemoryData(Key, Value).
	
	% Robot status percepts
	forall percept(posture(Posture)), bel(posture(OldPosture), OldPosture \= Posture)
		do delete(posture(OldPosture)) + insert(posture(Posture)).
	forall percept(isAwake(IsAwake))
		do {
			if bel(isAwake, IsAwake = false) then delete(isAwake).
			if bel(not(isAwake), IsAwake = true) then insert(isAwake).
		}
	
	if percept(stiffness(Stiffness)), bel(stiffness(OldStiffness), OldStiffness \= Stiffness)
		then delete(stiffness(OldStiffness)) + insert(stiffness(Stiffness)).
	
	forall percept(batteryCharge(Charge)), bel(batteryCharge(OldCharge), OldCharge \= Charge)
		do delete(batteryCharge(OldCharge)) + insert (batteryCharge(Charge)).
	forall percept(isCharging(IsCharging))
		do {
			if bel(isCharging, IsCharging = false) then delete(isCharging).
			if bel(not(isCharging), IsCharging = true) then insert(isCharging).
		}
		
	% Motion recording percept
	forall percept(motionRecording(Recording)), bel(currentTopic(T), currentState(S), generateKeyFromTopicAndState(T, S, ID)), not(bel(motionRecording(ID, Recording)))
		do insert(motionRecording(ID, Recording)).

	% Handling of event waiting logic...
	% ... for NAO events
	forall bel(event(E), waitingForEvent(E))
		do delete(waitingForEvent(E)).
	% ... for posture changes
	if bel(posture(Posture), waitingForPosture(Posture))
		then delete(waitingForPosture(Posture)).
	% ... for motion recordings
	if bel(motionRecording(ID, _), waitingForMotionRecording(ID))
		then delete(waitingForMotionRecording(ID)).
	
	% Handling timeouts
	if percept(timer(S, _)), not(bel(timeout(S)))
		then insert(timeout(S)).
	if bel(timeout(S)), not(percept(timer(S, _)))
		then delete(timeout(S)).
		
	% Motion recording timeout
	if bel(timeout(recordMotion)) then stopMotionRecording + wakeUp.
		
	%Audio recording percepts
	if percept(audioRecording(FileName)), bel(waitingForAudioFile(Tsource, Ssource))
		then loadAudio(FileName) + delete(waitingForAudio, waitingForAudioFile(Tsource, Ssource)) + insert(waitingForLoadedAudioID(Tsource, Ssource)).
	if percept(loadedAudioID(AudioID)), bel(waitingForLoadedAudioID(Tsource, Ssource))
		then insert(audioRecording(Tsource, Ssource, AudioID)) + delete(waitingForLoadedAudioID(Tsource, Ssource)).
	
	%Emotion detection percepts
	if percept(emotionDetected(Emotion)), bel(currentTopic(T), currentState(S), waitingForEmotion)
		then insert(emotion(T, S, Emotion)) + delete(waitingForEmotion) + log(Emotion) + stopWatching.
	
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Recognition and Repair            %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%%% SPEECH %%%
	
	% User responds. 
	if percept(intent(Intent, Params, _, Text, _)), bel(currentTopic(T), currentState(S), currentInputModality(speech), waitingForDetection)
		then {
				%If a user responded the agent can stop listening, delete its waiting flag, and insert the received speech text.
				if true then stopListening + delete(waitingForDetection).
								
				%Dialogflow recognizes intent. Retrieve answer.
				if bel(not(Intent='')) then processUserResponse(T, S, Intent, Text, Params).
				
				%Dialogflow does not recognize intent. Engage repair attempt.
				if bel(Intent='') then repair(T,S).				
		}
							
	% User does not respond.	
	if bel(event('IntentDetectionDone'), currentTopic(T), currentState(S), waitingForDetection)
		then {
			% If a user does not respond, listening is already finished. Delete its waiting flag.
			if true then delete(waitingForDetection).
			
			if bel(keyValue(T, S, additionalAttempt, true), currentAttempt(1)) then insert(additionalAttempt(T, S)).
			
			% If the question is open-endend no response can be processed, for any other question a repair attempt is appropriate.
			if bel(not(keyValue(T, S, type, openend))) then repair(T,S).
			if bel(keyValue(T, S, type, openend)) then processUserResponse(T, S, any, '', []).
		}
	
	%%% TOUCH %%%
	% An answer has been selected. Retrieve answer.
	if bel(currentTopic(T), currentState(S), state(T, S, question), waitingForAnswer, feetBumperEventAnswer(Intent))
		then processUserResponse(T, S, Intent, '', []).
	
	% No answer has been selected. Try a repair action.
	if bel(currentTopic(T), currentState(S), state(T, S, question), timeout(S), waitingForAnswer, currentInputModality(touch))
		then repair(T, S).
		
	%%% AUDIO RECORDING %%%
	% When timeout occurs while waiting for audio the audio recording is completed. Stop listening (and waiting for audio input). 
	if bel(event('IntentDetectionDone'), currentTopic(T), currentState(S), state(T, S, audioInput), waitingForAudio)
		then stopListening + disableRecording + delete(waitingForAudio).

	%%% EMOTION RECOGNITION %%%	
	% When timeout occurs while waiting for emotion, emotion detection is stopped.
	%TODO add appropriate event here
	if bel(currentTopic(T), currentState(S), state(T, S, emotion), waitingForEmotion, not(emotion(T, S,_)))
		then stopWatching + insert(emotion(T, S, 'fail')) + delete(waitingForEmotion).
		
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Answer processing			      %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%Given an answer(T, S, Ans, Details) and the type of question (yesno, input, branch, quiz) the answers should be processed differently.
	if bel(currentTopic(T), currentState(S), answer(T,S, Ans, Data), not(answerProcessed))
		then selectRobotResponse(T, S, Ans, Data).
		
	% If the state is a branching point, retrieve the decision and insert that as the nextCondition.
	if bel(currentTopic(T), currentState(S), state(T, S, branchingPoint), getBranchingPointDecision([T, S], Decision), nextCondition(Con))
		then delete(nextCondition(Con)) + insert(nextCondition(Decision)).
		
	% If an emotion is expected and present, set that as the next condition.
	if bel(currentTopic(T), currentState(S), state(T, S, emotion), emotion(T, S,Emotion), nextCondition(Con), not(Con=Emotion))
		then delete(nextCondition(Con)) + insert(nextCondition(Emotion)).
		
		
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Start recognition for answers, audio, and emotions  %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	% Question
	if bel(currentTopic(T), currentState(S), state(T, S, question), eventsCompleted, not(waitingForAnswer), not(answer(T, S, _, _)))
		then askQuestion(T, S).
		
	% Audiorecording
	if bel(currentTopic(T), currentState(S), state(T, S, audioInput), eventsCompleted, not(waitingForAudio), not(waitingForAudioFile(_, _)), not(audioRecording(T, S, _)), keyValue(T, S, recordTime, RT), Time is RT/1000)
		then enableRecording + startListening(Time) + insert(waitingForAudio, waitingForAudioFile(T, S)).
	
	% Emotion
	if bel(currentTopic(T), currentState(S), state(T, S, emotion), eventsCompleted, not(waitingForEmotion), not(emotion(T, S,_)))
		then startWatching(6) + insert(waitingForEmotion).
		
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% State transition handling            %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	% If state has been completed,  
	if bel(currentTopic(T), completed(S), nextCondition(C)) then performStateTransition(T, S, C).
	
}
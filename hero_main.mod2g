use 'util/*.pl' as knowledge.
use 'minidialogs/*.pl' as knowledge.
use 'minidialogs/co_creation/*.pl' as knowledge.
use 'minidialogs/hospital/*.pl' as knowledge.
use 'minidialogs/robotstories/*.pl' as knowledge.
use sessions as knowledge.
use hero_actions as actionspec.
use 'dialogMoves/performDialogMove' as module.
use 'userModel/*.mod2g' as module.

module hero_main {
	% Adapt minidialog goal if all the necessary information is loaded from the database.
	if not(goal(minidialogs([]))) then {
		if bel(useGui(true), not(waitingForGuiData(_)), waitingForInit) then initializeUserModel.
		
		if bel(waitingForHistory, not(waitingForEvent('ClearHistory')), userId(UserId)) then getDialogHistory(UserId) + getNarrativeHistory(UserId).
	
		if bel(not(waitingForInit), not(waitingForHistory), waitingForSession, memoryTasksCompleted, sessionId(SessionId), sessionOrder(SessionOrder), userId(UserId),
				nth1(SessionId, SessionOrder, SessionLabel), session(SessionLabel, MiniDialogs), nth0(0, MiniDialogs, FirstMinidialog, RestOfMinidialogs),
				dialogHistory(History), updateDialogHistory(FirstMinidialog, UpdatedHistory), currentMove(Move)) 
			then delete(waitingForSession, dialogHistory(History)) 
			+ insert(currentMinidialog(FirstMinidialog), minidialogs(RestOfMinidialogs), dialogHistory(UpdatedHistory))
			+ setDialogHistory(UserId, FirstMinidialog)
			+ setMoveHistory(UserId, Move)
			+ loadInteractantData(MiniDialogs) + adopt(minidialogs([])).
	}
	
	% Execute the move actions
	if bel(currentMinidialog(Minidialog), currentMove(Move), readyForAction(Minidialog, Move)) then performDialogMove(Minidialog, Move).
	
	%TODO: make pretty
	if bel(pause_act) then delete(pause_act) + say("Ik ga even pauze houden.") + rest + stopLedAnimation. 
	if bel(unpause_act) then delete(unpause_act, paused) + wakeUp + say("Laten we weer verder gaan.").
	if bel(stop_act) then delete(stop_act) + say("Weltrusten") + rest + stopLedAnimation.
}
	
	
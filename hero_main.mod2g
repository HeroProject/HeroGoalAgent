use 'util/*.pl' as knowledge.
use 'topics/*.pl' as knowledge.
use sessions as knowledge.
use hero_actions as actionspec.
use 'stateLogic/performStateActions' as module.
use 'memory/loadUserData' as module.

module hero_main {
	% Adapt topic goal if all the necessary information is loaded from the database. 
	if not(goal(topics([]))), bel(waitingForUserModelInit, memoryTasksCompleted, getUserModelValue('session_number', SessionNumber), sessionOrder(SessionOrder), 
		nth1(SessionNumber, SessionOrder, SessionID), session(SessionID, Topics), nth0(0, Topics, FirstTopic, RestOfTopics))
			then delete(waitingForUserModelInit) + insert(currentTopic(FirstTopic), topics(RestOfTopics), waitingForLoadedUserModel) 
				+ loadUserData(Topics) + adopt(topics([])).
	
	% Execute the state actions
	if bel(currentTopic(T), currentState(S), start, not(waitingForEvent(_)), memoryTasksCompleted) then 
	{
		if bel(not(audio(T, S, recorded, _))) then performStateActions(T, S).
		
		if bel(audio(T, S, recorded, [Tsource, Ssource]), audioRecording(Tsource, Ssource, _))
			then performStateActions(T, S).
	}
}
	
	
use 'util/*.pl' as knowledge.
use 'minidialogs/*.pl' as knowledge.
use 'minidialogs/co_creation/*.pl' as knowledge.
use agent_actions as actionspec.
use 'recognitionAndRepairPipeline/*.mod2g' as module.
use 'dialogManagement/*.mod2g' as module.
use 'userModel/*.mod2g' as module.

module agent_events {
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Percept and event processing      %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	% Robot event percepts
	forall percept(event(E)), not(bel(event(E)))
		do insert(event(E)).
	forall bel(event(E)), not(percept(event(E)))
		do delete(event(E)).
	
	if bel(event(pause)) then insert(paused, pause_act).
	if bel(waitForPause(PB), event(PB)) then insert(paused, pause_act, waitForUnPause(PB)) + delete(waitForPause(PB)).
	
	if bel(event(unpause)) then insert(unpause_act).
	if bel(waitForUnPause(PB), event(PB), not(pause_act)) then insert(unpause_act, waitForPause(PB)) + delete(waitForUnPause(PB)).
	
	if bel(event(stop)) then insert(stop_act).
		
	% Memory data percepts
	forall percept(memoryData(Key, Value))
		do handleMemoryData(Key, Value).
	
	% Robot status percepts
	forall percept(posture(Posture)), bel(posture(OldPosture), OldPosture \= Posture)
		do delete(posture(OldPosture)) + insert(posture(Posture)).
	forall percept(isAwake(IsAwake))
		do {
			if bel(isAwake, IsAwake = false) then delete(isAwake).
			if bel(not(isAwake), IsAwake = true) then insert(isAwake).
		}
		
	forall percept(batteryCharge(Charge)), bel(batteryCharge(OldCharge), OldCharge \= Charge)
		do delete(batteryCharge(OldCharge)) + insert (batteryCharge(Charge)).
	forall percept(isCharging(IsCharging))
		do {
			if bel(isCharging, IsCharging = false) then delete(isCharging).
			if bel(not(isCharging), IsCharging = true) then insert(isCharging).
		}
			
	forall percept(motionRecording(Recording)), bel(waitingForMotionRecording(ID), userModel(UserModel), 
														updateUserModel(ID, Recording, UserModel, NewUserModel), userId(UserId))
		do delete(userModel(UserModel), waitingForMotionRecording(ID)) 
			+ insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, Recording).
			
	% Handling of event waiting logic...
	% ... for NAO events
	forall bel(event(E), waitingForEvent(E))
		do delete(waitingForEvent(E)).
	
	% All event handlers trigger for all current events
	forall bel(event(E), eventListener(E, Result))
		do {
			% the resulting next condition is selected.
			if bel(nextCondition(Con)) then delete(eventListener(E, Result), nextCondition(Con)) + insert(nextCondition(Result)).
			
			% the remaining event listeners are deleted, because only one needs to be triggered.
			forall bel(eventListener(OtherEvent, OtherResult)) do delete(eventListener(OtherEvent, OtherResult), waitingForEvent(OtherEvent)).
		}
		
	% ... for posture changes
	if bel(posture(Posture), waitingForPosture(Posture))
		then delete(waitingForPosture(Posture)).
	
	% Handling timeouts
	if percept(timer(Move, _)), not(bel(timeout(Move)))
		then insert(timeout(Move)).
	if bel(timeout(Move)), not(percept(timer(Move, _)))
		then delete(timeout(Move)).
		
	% Motion recording timeout
	if bel(timeout(recordMotion)) then stopMotionRecording + wakeUp.
	
	% waitingForTimer
	if bel(timeout(waitTimer)) then delete(waitingForTimer).
	
	% Go timer
	if bel(timeout(goTimer)) then {
		% set the next condition to timer if the go timer timeouts
		if bel(nextCondition(Con)) then delete(nextCondition(Con)) + insert(nextCondition('timer')).
		
		% Remove all the event listeners waiting to be triggered. This timeout makes them obsolete.
		forall bel(eventListener(Event, Result)) do delete(eventListener(Event, Result), waitingForEvent(Event)).	
	} 
	
	% delay timer
	if bel(timeout(delay)) then delete(waitingForDelay).
	
	if bel(timeout(maxInteractionDuration)) then delete(activeInteraction).
	
	% Event fail safe TODO: fix this
	if bel(timeout(eventFailSafe), not(eventListener(_, _))) then {
		forall bel(waitingForEvent(E)) do delete(waitingForEvent(E)).
		%if bel(waitingForAudio) then delete(waitingForAudio).
		%if bel(currentMinidialog(Minidialog), currentMove(Move)) then log(eventFailSafe(Minidialog, Move)).
	}
	
	%Audio recording percepts
	forall percept(audioRecording(FileName)), bel(waitingForAudioFile(ID), userId(UserId), userModel(UserModel), updateUserModel(ID, FileName, UserModel, NewUserModel))
		do delete(waitingForAudioFile(ID), userModel(UserModel)) + insert(userModel(NewUserModel)) + setInteractantData(UserId, ID, FileName).
	
	%Emotion detection percepts
	%forall percept(emotionDetected(Emotion)), bel(currentMinidialog(Minidialog), currentMove(Move), waitingForEmotion)
		%do insert(emotion(Minidialog, Move, Emotion)) + delete(waitingForEmotion) + log(Emotion) + stopWatching.
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Recognition and Repair            %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%%% SPEECH %%%
	
	% User responded (full intent).
	if percept(intent(Intent, Params, _, Text, _)), bel(currentMinidialog(Minidialog), currentMove(Move), currentInputModality(speech), waitingForDetection)
		then {
				%If a user responded the agent can stop listening, delete its waiting flag, and insert the received speech text.				
				if true then stopListening + delete(waitingForDetection).
								
				%Dialogflow recognizes intent. Retrieve answer.
				if bel(Intent \= '') then processUserResponse(Minidialog, Move, Intent, Text, Params).
				
				%Dialogflow does not recognize intent. Engage repair attempt.
				if bel(Intent = '') then {
					if not(bel(keyValue(Minidialog, Move, type, openend))) then repair(Minidialog, Move).
					if bel(keyValue(Minidialog, Move, type, openend)) then processUserResponse(Minidialog, Move, any, Text, []).
				} 				
		}
	
	% User responded (try quick transcript matching).
	if percept(transcript(Text)), bel(currentMinidialog(Minidialog), currentMove(Move), waitingForDetection)
		then {
			if bel(keyValue(Minidialog, Move, type, yesno), answer_yes_synonyms(Yes), string_match_word_list(Text, Yes, _)) 
				then stopListening + delete(waitingForDetection) + processUserResponse(Minidialog, Move, answer_yes, Text, []).
			if bel(keyValue(Minidialog, Move, type, yesno), answer_no_synonyms(No), string_match_word_list(Text, No, _))
				then stopListening + delete(waitingForDetection) + processUserResponse(Minidialog, Move, answer_no, Text, []).
			if bel(keyValue(Minidialog, Move, type, yesno), answer_dontknow_synonyms(Dontknow), string_match_word_list(Text, Dontknow, _))
				then stopListening + delete(waitingForDetection) + processUserResponse(Minidialog, Move, answer_dontknow, Text, []).
			if bel(not(keyValue(Minidialog, Move, type, yesno)), keyListValues(Minidialog, Move, [context, options, fast], [Intent, Options, yes]), string_match_word_list(Text, Options, Ans))
				then stopListening + delete(waitingForDetection) + processUserResponse(Minidialog, Move, Intent, Text, [answer=Ans]).
		}
										
	% User responded on the tablet (for useTablet=always).
	if percept(answer(Text)), bel(currentMinidialog(Minidialog), currentMove(Move), waitingForDetection,
									keyValue(Minidialog, Move, context, Intent), downcase_atom(Text, DT))
		then stopListening + delete(waitingForDetection) + processUserResponse(Minidialog, Move, Intent, Text, [answer=DT]).
							
	% User did not respond.	
	if bel(event('IntentDetectionDone'), currentMinidialog(Minidialog), currentMove(Move), waitingForDetection)
		then {
			% If a user does not respond, listening is already finished. Delete its waiting flag.
			if true then delete(waitingForDetection).
			
			if bel(getAdditionalAttempt(Minidialog, Move), currentAttempt(1)) then insert(additionalAttempt(Minidialog, Move)).
			
			% If the question is open-endend no response can be processed, for any other question a repair attempt is appropriate.
			if not(bel(keyValue(Minidialog, Move, type, openend))) then repair(Minidialog,Move).
			if bel(keyValue(Minidialog, Move, type, openend)) then processUserResponse(Minidialog, Move, any, '', []).
		}
	
	%%% FEET %%%
	% An answer has been selected. Retrieve answer.
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, question), waitingForAnswer, feetBumperEventAnswer(Intent))
		then processUserResponse(Minidialog, Move, Intent, '', []).
	
	% No answer has been selected. Try a repair action.
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, question), timeout(Move), waitingForAnswer, currentInputModality(feet))
		then repair(Minidialog, Move).
	
	%%% TABLET %%%
	if percept(answer(Text)), bel(currentMinidialog(Minidialog), currentMove(Move), waitingForAnswer)
		then processUserResponse(Minidialog, Move, Text, Text, []).
	
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, question), timeout(Move), waitingForAnswer, currentInputModality(tablet))
		then repair(Minidialog, Move).
	
	%%% AUDIO RECORDING %%%
	% When timeout occurs while waiting for audio the audio recording is completed. Stop listening (and waiting for audio input). 
	if bel(event('IntentDetectionDone'), currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, audioInput))
		then stopListening + disableRecording.

	%%% EMOTION RECOGNITION %%%	
	% When timeout occurs while waiting for emotion, emotion detection is stopped.
	%TODO add appropriate event here
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, emotion), waitingForEmotion, not(emotion(Minidialog, Move,_)))
		then stopWatching + insert(emotion(Minidialog, Move, 'fail')) + delete(waitingForEmotion).
		
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Answer processing			      %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	%Given an answer(Minidialog, Move, Ans, Details) and the type of question (yesno, input, quiz) the answers should be processed differently.
	if bel(currentMinidialog(Minidialog), currentMove(Move), answer(Minidialog,Move, Ans, Data), not(answerProcessed))
		then selectRobotResponse(Minidialog, Move, Ans, Data).
		
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, branchingPoint, Source), getUserModelValue(Source, Decision), nextCondition(Con)) then {
		if bel(next(Minidialog, Move, Decision, _))
			then delete(nextCondition(Con)) + insert(nextCondition(Decision)).
		if bel(not(next(Minidialog, Move, Decision, _)), Decision \= "fail", next(Minidialog, Move, "_others", _))
			then delete(nextCondition(Con)) + insert(nextCondition("_others")).
		if bel(not(next(Minidialog, Move, Decision, _)), Decision \= "fail", not(next(Minidialog, Move, "_others", _)))
			then delete(nextCondition(Con)) + insert(nextCondition("success")).
	}
		
		
	% If an emotion is expected and present, set that as the next condition.
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, emotion), emotion(Minidialog, Move,Emotion), nextCondition(Con), not(Con=Emotion))
		then delete(nextCondition(Con)) + insert(nextCondition(Emotion)).
		
		
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Start recognition for answers, audio, and emotions  %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	% Question
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, question), eventsCompleted, 
		not(waitingForAnswer), not(answer(Minidialog, Move, _, _)), not(waitingForDelay))
		then askQuestion(Minidialog, Move).
		
	% Audiorecording
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, audioInput), eventsCompleted,
			keyListValues(Minidialog, Move, [recordTime, umVariable], [RT, ID]), not(waitingForSoundAnim(_)),
			not(waitingForAudioFile(ID)),  not(getUserModelValue(ID, _)), Time is RT/1000)
		then enableRecording + startListening(Time) + insert(waitingForAudioFile(ID)).
	
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, audioInput), eventsCompleted, waitingForSoundAnim(ID),
			soundConfig(ID, RT), not(waitingForAudioFile(ID)), not(getUserModelValue(ID, _)), Time is RT/1000)
		then enableRecording + startListening(Time) + insert(waitingForAudioFile(ID)).
	
	% Emotion
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, emotion), eventsCompleted, not(waitingForEmotion), not(emotion(Minidialog, Move,_)))
		then startWatching(6) + insert(waitingForEmotion).
	
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Wait for event (e.g. button press) in a say move   %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	
	if bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, say), keyValue(Minidialog, Move, nextTablet, Next), 
			not(waitingForTablet(_)), button(Next, Button), html(Button, Html), eventsCompleted)
		then insert(waitingForTablet(Next)) + renderPage(Html).
	
	% If the next event is available, clear this move.
	if  percept(answer(Next)), bel(currentMinidialog(Minidialog), currentMove(Move), move(Minidialog, Move, say), keyValue(Minidialog, Move, nextTablet, Next),	
			waitingForTablet(Next), html('', Html))
		then delete(waitingForTablet(Next)) + renderPage(Html).

	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%%% Move transition handling            %%%
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	% If move has been completed,  
	if bel(currentMinidialog(Minidialog), completed(Move), nextCondition(C)) then nextDialogMove(Minidialog, Move, C).
	
}